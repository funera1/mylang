// 文法
// ルール
// ::= は左辺が非終端記号で右辺がその非終端記号から遷移する(非)終端記号列
// := は左辺が非終端記号で右辺が終端記号１つ
// 全ての非終端記号は必ず終端記号のみの列に収束する必要がある
// 左再帰の解消が必要

PROGRAM ::= LBRACKET COMPOUND RBRACKET
COMPOUND ::= STATEMENT COMPOUND
COMPOUND ::= EPS
STATEMENT ::= DECLARATION_STATEMENT
STATEMENT ::= ASSIGN_STATEMENT
STATEMENT ::= IF_STATEMENT
DECLARATION_STATEMENT ::= INT ID SEMICOLON
ASSIGN_STATEMENT ::= ID ASSIGN EXPR SEMICOLON
IF_STATEMENT ::= IF LPAREN EXPR RPAREN LBRACKET COMPOUND RBRACKET

// 数式
EXPR ::= TERM EXPR_
EXPR_ ::= PLUS TERM EXPR_
EXPR_ ::= MINUS TERM EXPR_
EXPR_ ::= EPS
TERM ::= FACTOR TERM_
TERM_ ::= MULTI FACTOR TERM_
TERM_ ::= DIVISION FACTOR TERM_
TERM_ ::= EPS
FACTOR ::= LPAREN EXPR RPAREN
FACTOR ::= NUMBER
FACTOR ::= ID
PLUS := +
MINUS := -
MULTI := *
DIVISION := /

LBRACKET := {
RBRACKET := }
LPAREN := (
RPAREN := )
SEMICOLON := ;
INT := int
ASSIGN := =
IF := if
ID := #id
NUMBER := #number
EPS := #eps
END := $
