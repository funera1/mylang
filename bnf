; // 文法
; // ルール
; // ::= は左辺が非終端記号で右辺がその非終端記号から遷移する(非)終端記号列
; // := は左辺が非終端記号で右辺が終端記号１つ
; // 全ての非終端記号は必ず終端記号のみの列に収束する必要がある
; // 左再帰の解消が必要

PROGRAM ::= LBRACKET COMPOUND RBRACKET
COMPOUND ::= STATEMENT COMPOUND
COMPOUND ::= STATEMENT

; // STATEMENT
STATEMENT ::= DECLARATION_STATEMENT
STATEMENT ::= ASSIGN_STATEMENT
STATEMENT ::= IF_STATEMENT
STATEMENT ::= WHILE_STATEMENT
STATEMENT ::= PRINT_STATEMENT

; 各STATEMENTの説明
DECLARATION_STATEMENT ::= INT ID SEMICOLON
ASSIGN_STATEMENT ::= ID ASSIGN EXPR SEMICOLON
IF_STATEMENT ::= IF LPAREN EXPR RPAREN LBRACKET COMPOUND RBRACKET
WHILE_STATEMENT ::= WHILE LPAREN EXPR RPAREN LBRACKET COMPOUND RBRACKET
PRINT_STATEMENT ::= PRINT LPAREN EXPR RPAREN SEMICOLON

; // 数式

EXPR ::= TERM PLUS EXPR
EXPR ::= TERM MINUS EXPR
EXPR ::= TERM
TERM ::= FACTOR MULTI  TERM
TERM ::= FACTOR DIVISION TERM
TERM ::= FACTOR
FACTOR ::= LPAREN EXPR RPAREN
FACTOR ::= NUMBER
FACTOR ::= ID
PLUS := +
MINUS := -
MULTI := *
DIVISION := /

LBRACKET := {
RBRACKET := }
LPAREN := (
RPAREN := )
SEMICOLON := ;
INT := int
ASSIGN := =
IF := if
ID := #id
NUMBER := #number
END := $
WHILE := while
PRINT := print